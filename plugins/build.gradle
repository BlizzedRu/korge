buildscript {
	repositories {
		maven { url "https://plugins.gradle.org/m2/" }
	}
	dependencies {
		classpath "com.gradle.publish:plugin-publish-plugin:0.9.6"
	}
}

plugins {
	id 'kotlin-multiplatform' version '1.3.11'
	id 'com.moowork.node' version '1.2.0'
}

new File("korge-build/src/main/kotlin/com/soywiz/korge/build/BuildVersions.kt").write("""
package com.soywiz.korge.build

object BuildVersions {
	const val KORMA = "$kormaVersion"
	const val KORIO = "$korioVersion"
	const val KORIM = "$korimVersion"
	const val KORAU = "$korauVersion"
	const val KORUI = "$koruiVersion"
	const val KORGE = "$korgeVersion"
}
""")

allprojects {
	group 'com.soywiz'
	version projectVersion

	repositories {
		mavenLocal()
		jcenter()
		maven { url 'https://plugins.gradle.org/m2/' }
		maven { url "https://dl.bintray.com/soywiz/soywiz" }
	}

	apply plugin: 'maven'
	apply plugin: 'maven-publish'

	def pomBaseData = {
		licenses {
			license {
				name project.property("project.license.name")
				url project.property("project.license.url")
			}
		}
		scm {
			url project.property("project.scm.url")
		}
	}

	def generatePom = { pom ->
		pom.withXml {
			def root = it.asNode()
			root.appendNode('name', project.name)
			root.appendNode('description', project.property("project.description"))
			root.appendNode('url', project.property("project.scm.url"))
			root.children().last() + pomBaseData
		}
	}

	ext.generatePom = generatePom

	def publishUser = rootProject.findProperty('BINTRAY_USER') ?: project.findProperty('bintrayUser') ?: System.getenv('BINTRAY_USER')
	def publishPassword = rootProject.findProperty('BINTRAY_KEY') ?: project.findProperty('bintrayApiKey') ?: System.getenv('BINTRAY_API_KEY')

	if (publishUser && publishPassword) {
		publishing {
			repositories {
				maven {
					credentials {
						username publishUser
						password publishPassword
					}
					url "https://api.bintray.com/maven/soywiz/soywiz/${project.property('project.package')}/"
					//url "https://api.bintray.com/content/soywiz/soywiz/korlibs/$projectVersion"
				}
			}

			configure(publications) {
				generatePom(pom)
			}
		}
	}
}
